#version 450
#extension GL_ARB_separate_shader_objects : enable
//
// (C) Copyright 2010 Patrick Cozzi and Deron Ohlarik
//
// Distributed under the MIT License.
// See License.txt or http://www.opensource.org/licenses/mit-license.php.
//
layout(set=0, binding = 0) uniform UniformBufferObject {
    mat4 prj;         
    vec3 CameraEyeSquared;
    float spa1;
    vec3 CameraEye;   
    float spa2;
    vec3 CameraLightPosition;
    float spa3;
    vec4 DiffuseSpecularAmbientShininess;
    vec3 GlobeOneOverRadiiSquared;
    float spa4;
} ubo;


const bool UseAverageDepth=false;
const float PI = 3.14159265;
const float og_oneOverTwoPi=1.0/(2*PI);
const float og_oneOverPi=1.0/PI;


layout(location = 0) in vec3 worldPosition;
layout(location = 0) out vec3 fragmentColor;

layout(set = 1, binding = 0) uniform texture2D Tex;
layout(set = 1, binding = 1) uniform sampler Samp;


struct Intersection
{
    bool  Intersects;
    float NearTime;         // Along ray
    float FarTime;          // Along ray
};

//
// Assumes ellipsoid is at (0, 0, 0)
//
Intersection RayIntersectEllipsoid(vec3 rayOrigin, vec3 rayOriginSquared, vec3 rayDirection, vec3 oneOverEllipsoidRadiiSquared)
{
    float a = dot(rayDirection*rayDirection,oneOverEllipsoidRadiiSquared);
    float b = 2.0 * dot(rayOrigin*rayDirection,oneOverEllipsoidRadiiSquared);
    float c = dot(rayOrigin*rayOrigin, oneOverEllipsoidRadiiSquared) - 1.0;
    float discriminant = b * b - 4.0 * a * c;

    //return Intersection(false, 0.0, 0.0);
    if (discriminant < 0.0)
    {
        return Intersection(false, 0.0, 0.0);
    }
    else if (discriminant == 0.0)
    {
        float time = -0.5 * b / a;
        return Intersection(true, time, time);
    }

    float t = -0.5 * b / a;
    float deta=-0.5*sqrt(discriminant)/a;
    float root1 = t+deta;
    float root2 = t-deta;

    return Intersection(true, min(root1, root2), max(root1, root2));
}

float ComputeWorldPositionDepth(vec3 position, mat4 prj)
{ 
    vec4 v = prj * vec4(position, 1.0);   // clip coordinates
    v.z /= v.w;                                             // normalized device coordinates
    v.z = (v.z + 1.0) * 0.5;
    return v.z;
}

vec3 GeodeticSurfaceNormal(vec3 positionOnEllipsoid, vec3 oneOverEllipsoidRadiiSquared)
{
    return normalize(positionOnEllipsoid * oneOverEllipsoidRadiiSquared);
}

float LightIntensity(vec3 normal, vec3 toLight, vec3 toEye, vec4 diffuseSpecularAmbientShininess)
{
    vec3 toReflectedLight = reflect(-toLight, normal);

    float diffuse = max(dot(toLight, normal), 0.0);
    float specular = max(dot(toReflectedLight, toEye), 0.0);
    specular = pow(specular, diffuseSpecularAmbientShininess.w);

    return (diffuseSpecularAmbientShininess.x * diffuse) +
           (diffuseSpecularAmbientShininess.y * specular) +
            diffuseSpecularAmbientShininess.z;
}

vec2 ComputeTextureCoordinates(vec3 normal)
{
    return vec2(atan(normal.x, normal.z) * og_oneOverTwoPi+0.5, asin(normal.y) * og_oneOverPi - 0.5);
}

void main()
{
    vec3 rayDirection = normalize(worldPosition - ubo.CameraEye);
    Intersection i = RayIntersectEllipsoid(ubo.CameraEye, ubo.CameraEyeSquared, rayDirection, ubo.GlobeOneOverRadiiSquared);

    if (i.Intersects)
    {
        vec3 position = ubo.CameraEye + (i.NearTime * rayDirection);
        vec3 normal = GeodeticSurfaceNormal(position, ubo.GlobeOneOverRadiiSquared);

        vec3 toLight = normalize(ubo.CameraLightPosition - position);
        vec3 toEye = normalize(ubo.CameraEye - position);
        float intensity = LightIntensity(normal, toLight, toEye, ubo.DiffuseSpecularAmbientShininess);

		vec2 textureCoordinate=ComputeTextureCoordinates(normal);
		vec2 distanceToLine = mod(textureCoordinate, vec2(0.1,0.1));
        vec2 dx = abs(dFdx(textureCoordinate));
        vec2 dy = abs(dFdy(textureCoordinate));
        vec2 dF = vec2(max(dx.s, dy.s), max(dx.t, dy.t)) * 1.0;

		if (any(lessThan(distanceToLine, dF)))
    {
        fragmentColor = vec3(1.0, 1.0, 1.0);
    }else
	{
	    fragmentColor = intensity * texture(sampler2D(Tex,Samp), textureCoordinate).rgb;
	}

        

        if (UseAverageDepth)
        {
            position = ubo.CameraEye + (mix(i.NearTime, i.FarTime, 0.5) * rayDirection);
        }

        gl_FragDepth = ComputeWorldPositionDepth(position, ubo.prj);
    }
    else
    {
        discard;
    }
}